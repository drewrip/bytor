use std::str::FromStr;
use std::sync::Arc;
use crate::ast::*;
use crate::types::*;
use crate::symbol::{Symbol, Var};

grammar;

pub Root: Arc<Root> = {
    <pre_b:Stmts> <prog:Program> <post_b:Stmts> => Arc::new(
        Root{preblock: pre_b, program: prog, postblock: post_b}
    ),
};

pub Stmts: Vec<Arc<Stmt>> = {
    <v:(<Stmt>)*> => v,
}

pub Exprs = Comma<Expr>;
pub Args = Exprs;

Semicolon<T>: Vec<T> = {
    <mut v:(<T> ";")*> <e:(<T> ";")?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

pub IfCase: Arc<IfCase> = {
    "if" <e:Expr> "then" <b:Stmts> => Arc::new(IfCase {condition: e, block: b}),
};

pub ElseIfCase: Arc<IfCase> = {
    "else" "if" <e:Expr> "then" <b:Stmts> => Arc::new(IfCase {condition: e, block: b}),
};

pub ElseCase: Arc<IfCase> = {
    "else" "then" <b:Stmts> => Arc::new(IfCase {condition: Arc::new(Expr::Term(Arc::new(Term::Bool(true)))), block: b}),
};

pub Stmt: Arc<Stmt> = {
    "let" <i:Id> "=" <e:Expr> ";" => Arc::new(
        Stmt::Assign(
          Symbol{ident: i},
          Arc::new(Var{type_t: Type::Unknown, node: Node::ExprNode(e.clone())}),
          e
        )
    ),
    "let" <i:Id> ":" <t:Type> "=" <e:Expr> ";" => Arc::new(
        Stmt::Assign(
          Symbol{ident: i},
          Arc::new(Var{type_t: t, node: Node::ExprNode(e.clone())}),
          e
        )
    ),
    <i:Id> <ao:AssignOp> <e:Expr> ";" => Arc::new(
        Stmt::Reassign(
          Symbol{ident: i},
          Arc::new(Var{type_t: Type::Unknown, node: Node::ExprNode(e.clone())}),
          ao,
          e
        )
    ),
    <ifcase:IfCase> <elseifcases:ElseIfCase*> <elsecase:ElseCase?> "end" => {
        let elsecases: Vec<Arc<IfCase>> = match elsecase {
            Some(elsecasepresent) => vec![elsecasepresent],
            None => vec![],
        };
        Arc::new(Stmt::If(vec![vec![ifcase], elseifcases, elsecases].concat()))
    },
    <id:Id> "(" <args:Args> ")" ";" => Arc::new(Stmt::Call(Symbol{ident: id}, Arc::new(args))),
    <f:Func> => Arc::new(Stmt::FuncDef(f)),
};

pub Program: Arc<Program> = {
    "program" <id:Id> <b:Stmts> "end" => Arc::new(Program::NoWith(Symbol{ident: id}, b)),
    "program" <id:Id> <w:With> <b:Stmts> "end" => Arc::new(Program::With(Symbol{ident: id}, w, b)),
};

pub With: With = {
    "with" "(" <w:WithVars> ")" => w,
};

pub WithVars = Comma<WithVar>;

pub WithVar: Arc<WithVar> = {
    "mut" <id:Id> => Arc::new(WithVar::Mut(Symbol{ident: id})),
    <id:Id> => Arc::new(WithVar::Imm(Symbol{ident: id})),
};

pub Params: Params = Comma<Param>;

pub Param: Arc<Param> = {
    <id:Id> ":" <t:Type> => Arc::new(Param{type_t: t, ident: id}),
};

pub Func: Arc<Func> = {
    "function" <id:Id> "(" <p:Params> ")" <t:("->" <Type>)?> <w:With?> <b:Stmts> "end" => {
        let ret = match t {
            None => Type::Nil,
            Some(t) => t,
        };
        let with = match w {
            None => vec![],
            Some(w) => w,
        };
        Arc::new(Func{ret_t: ret, params: p, with: with, ident: id, block: b})
    },
};

pub Expr: Arc<Expr> = {
    #[precedence(level="0")] // Highest precedence
    <t:Term> => Arc::new(Expr::Term(t)),
    <id:Id> "(" <args:Args> ")" => Arc::new(Expr::Call(Symbol{ident: id}, Arc::new(args))),
    #[precedence(level="1")] #[assoc(side="left")]
    <l:Expr> "*" <r:Expr> => Arc::new(Expr::Mult(l, r)),
    <l:Expr> "/" <r:Expr> => Arc::new(Expr::Div(l, r)),
    #[precedence(level="2")] #[assoc(side="left")]
    <l:Expr> "+" <r:Expr> => Arc::new(Expr::Add(l, r)),
    <l:Expr> "-" <r:Expr> => Arc::new(Expr::Sub(l, r)),
    #[precedence(level="3")] #[assoc(side="left")]
    <l:Expr> "==" <r:Expr> => Arc::new(Expr::Eq(l, r)),
    <l:Expr> "!=" <r:Expr> => Arc::new(Expr::Neq(l, r)),
    <l:Expr> "<=" <r:Expr> => Arc::new(Expr::Leq(l, r)),
    <l:Expr> ">=" <r:Expr> => Arc::new(Expr::Geq(l, r)),
    <l:Expr> "<" <r:Expr> => Arc::new(Expr::LessThan(l, r)),
    <l:Expr> ">" <r:Expr> => Arc::new(Expr::GreaterThan(l, r)),
};

pub Term: Arc<Term> = {
    <id:Id> => Arc::new(Term::Id(id)),
    <n:Num> => Arc::new(Term::Num(n)),
    <b:Bool> => Arc::new(Term::Bool(b)),
    "(" <t:Expr> ")" => Arc::new(Term::Expr(t)),
};

pub Bool: bool = {
    "true" => true,
    "false" => false,
}

Num: i32 = r"[0-9]+" => i32::from_str(<>).unwrap();

Id: String = r"([a-zA-Z_])([\w\d_])*" => String::from(<>);

pub AssignOp: AssignOp = {
    "=" => AssignOp::Assign,
    "+=" => AssignOp::AddAssign,
    "-=" => AssignOp::SubAssign,
    "*=" => AssignOp::MultAssign,
    "/=" => AssignOp::DivAssign,
};

pub Type: Type = {
    "int64" => Type::Int64,
    "int32" => Type::Int32,
    "uint64" => Type::UInt64,
    "uint32" => Type::UInt32,
    "float64" => Type::Float64,
    "float32" => Type::Float32,
    "bool" => Type::Bool,
    "Nil" => Type::Nil,
}
